| CHIP Number  | < Creator must leave this blank. Editor will assign a number.>                                         |
| :----------- | :----------------------------------------------------------------------------------------------------- |
| Title        | Splitter Puzzle                                                                                        |
| Description  | A standard puzzle for splitting a coin according to a predefined splitting schema.                     |
| Author       | [Andreas Greimel](https://github.com/greimela) ([@acevail\_](https://twitter.com/acevail_) on Twitter) |
| Editor       | [Dan Perry](https://github.com/danieljperry)                                                           |
| Comments-URI | https://github.com/Chia-Network/chips/pull/30                                                          |
| Status       | Draft                                                                                                  |
| Category     | Informational                                                                                          |
| Sub-Category | Informative                                                                                            |
| Created      | 2022-09-03                                                                                             |
| Dependencies | None                                                                                                   |

## Abstract

This proposal aims to define a standard puzzle for splitting a coin according to a predefined splitting schema

## Motivation

One of the great features of NFT1 is the built-in payment of royalties.
It is very flexible, since any address and therefore any puzzle can be used as royalty recipient.
Many people in the community have requested the option to split the royalty payment between different recipients.
For example, a share should be sent to a charity, or multiple artists want to share the royalties automatically.
Having this features available increases the value of the Chia NFT ecosystem as a whole.

## Backwards Compatibility

This proposal does not have any backwards incompatibilities.

## Rationale

There have already been approaches to split royalties in the Chia ecosystem. The shortcoming of the existing approaches is a dependence on a single actor to trigger the royalty split.

The solution outlined in this propsal ensures independence from any single actor. This means that any observer of the Chia blockchain is able to trigger the royalty split, including the users wallet or an NFT marketplace.

The core is a simple Chialisp puzzle without many moving parts. It includes a payout scheme defining how the royalty should be split. The payment scheme is fixed and can not be altered by third parties. Anyone is able to trigger the royalty payout for any NFT.

## Specification

An example payout scheme could look like this

| Recipient Address                                              | Share      |
| :------------------------------------------------------------- | :--------- |
| xch18qt2ju2sj3k8w3290az6flkkc95fqcmcg7edl90ns0jrjav8xttsfmtqfp | 80         |
| xch1p9e3l3ttl7qrrhy6zmmqmjm0v33fvrxhd494yv7at0ppd97hljns4uw464 | 20         |

The proposed Chialisp puzzle looks like this.

```
(mod (PAYOUT_SCHEME my_amount)

  (include condition_codes.clib)
  (include curry-and-treehash.clinc)

  (defun-inline calculate_share (total_amount share total_shares)
    (f (divmod (* total_amount share) total_shares))
  )

  (defun-inline get_amount (payout_scheme_item total_amount total_shares)
    (calculate_share total_amount (f (r payout_scheme_item)) total_shares)
  )

  (defun-inline get_puzhash (payout_scheme_item)
    (f payout_scheme_item)
  )

  (defun-inline ignore_zero_amount (condition)
    (if (> (f (r (r condition))) 0)
      condition
      ()
    )
  )

   ; Loop through the payout scheme and create coins
  (defun split_amount_and_create_coins (PAYOUT_SCHEME total_amount total_shares remaining_amount)
    (c
      (ignore_zero_amount 
        (list
          CREATE_COIN
          (get_puzhash (f PAYOUT_SCHEME))
          (if (r PAYOUT_SCHEME) (get_amount (f PAYOUT_SCHEME) total_amount total_shares) remaining_amount)
        )
      )
      (if (r PAYOUT_SCHEME)
        (split_amount_and_create_coins 
          (r PAYOUT_SCHEME) 
          total_amount 
          total_shares 
          (- remaining_amount (get_amount (f PAYOUT_SCHEME) total_amount total_shares))
        )
        ()
      )
    )
  )

  ; calculate the sum of all shares to later determine the share of each participant
  (defun count_shares (PAYOUT_SCHEME)
    (if PAYOUT_SCHEME
      (+
        (f (r (f PAYOUT_SCHEME)))
        (count_shares (r PAYOUT_SCHEME))
      )
      0
    )
  )

  ; remove empty () members
  (defun squish (the_list)
    (if (l the_list)
      (if (not (l (f the_list)))
        (squish (r the_list))
        (c
          (f the_list)
          (squish (r the_list))
        )
      )
      ()
    )
  )

  ; main
  (c
    (list CREATE_COIN_ANNOUNCEMENT my_amount)
    (c
      (list ASSERT_MY_AMOUNT my_amount)
      (squish (split_amount_and_create_coins PAYOUT_SCHEME my_amount (count_shares PAYOUT_SCHEME) my_amount))
    )
  )
)
```

The payout scheme `PAYOUT_SCHEME` is curried into this puzzle, to make it immutable. 
Using the above scheme as an example, it looks like this:
```
((0x3816a97150946c7745457f45a4fed6c16890637847b2df95f383e439758732d7 80) (0x09731fc56bff8031dc9a16f60dcb6f6462960cd76d4b5233dd5bc21697d7fca7 20)
```

The only solution parameter needed on spend is the actual amount of the coin.

On spend, the puzzle steps through the entries of the `PAYOUT_SCHEME` and creates a new coin per entry. The amount of the new coins will be a share of the total amount, determined by the share defined in the `PAYOUT_SCHEME`. The total number of shares is defined as the  sum of all individual shares in the scheme and will be calculated by the puzzle.

This means the share distributions `(1,1)` and `(50,50)` and `(5000, 5000)` are equivalent.

If the created coins do not use up the full coin amount, the rest is added to the last recipient in the payout scheme. This difference is at most NUMBER_OF_RECIPIENTS mojos, therefore its not worth the effort to construct a more complicated mechanism. It is important that the full coin amount is being reissued, otherwise it breaks CAT coins.

## Test Cases

The unit test for the split Chialisp puzzle is located in the `greimela/chialisp` GitHub repository: [royalty_share/tests/test_p2_royalty_share_arbitrary_shares.py](https://github.com/greimela/chialisp/blob/main/royalty_share/tests/test_p2_royalty_share_arbitrary_shares.py).

<!-- A more extensive integration test using NFT offers can be found here: [tests/wallet/nft_wallet/test_nft_royalty_split.clvm](https://github.com/greimela/chia-blockchain/blob/royalty_split/tests/wallet/nft_wallet/test_nft_royalty_split.py). -->

## Reference Implementation

The Chialisp puzzle itself is located in the `greimela/chialisp` GitHub repository: [royalty_share/clsp/p2_royalty_share_arbitrary_shares/p2_royalty_share_arbitrary_shares_rest_to_last.clsp](https://github.com/greimela/chialisp/blob/main/royalty_share/clsp/p2_royalty_share_arbitrary_shares/p2_royalty_share_arbitrary_shares_rest_to_last.clsp).

This repository is a fork of the Chialisp repository by @trgarrett, which already contains some driver code for this and other royalty puzzles.
The plan is to unify all those royalty puzzles into one standard.

## Security

The Chialisp code has been covered by unit tests, but has not been audited yet by Chia Network or the broader community.

Potential risks:

- Royalty recipient can be altered
  - The royalty recipients are curried into the puzzle and the only parameter in the solution is the amount. So the payout scheme can not be altered after it has been created.
- Royalty payout can be stalled
  - Since the royalty payment can be triggered by any party on a permissionless blockchain, nobody can stall or prevent this. 
- Info to construct split puzzle can get lost
  - As soon as one particular split occurs for the first time, the puzzle is stored on the blockchain. Also, it is in the best interest of recipients of the split to store the payout scheme in order to get paid in the future.

## Additional Assets

No additional assets.

## Copyright

Copyright and related rights waived via [CC0](https://creativecommons.org/publicdomain/zero/1.0/).
